#!r6rs
;;lazy functions
(library (pi lazy)
         (export lazy-list lazy-pair? lazy-equal? lazy-list->list list->lazy-list lazy-null? lazy-append lazy-null lazy-repeat lazy-map lazy-pair)
         (import (rnrs)
                 (church readable-scheme))
         (define lazy-null '())
         (define lazy-null? null?)
         (define (lazy-pair a b) (lambda (tag) (if (eq? tag 'first) a (if (eq? tag 'rest) b 'lazy-pair))))
         (define (lazy-pair? a) (if (procedure? a) (eq? 'lazy-pair (a 'type?)) false))

         (define lazy-list (lambda args (if (pair? args) (lazy-pair (first args) (apply lazy-list (rest args))) args)))

         ;;returns false if finds missmatch, otherwise returns amount of sexprs matched.
         (define (seq-sexpr-equal? t1 t2 depth)
           (if (= depth 0)
               0
               (if (and (lazy-pair? t1) (lazy-pair? t2))
                   (let ((left (seq-sexpr-equal? (t1 'first) (t2 'first) (- depth 1))))
                     (if (eq? false left)
                         false
                         (seq-sexpr-equal? (t1 'rest) (t2 'rest) left)))
                   (if (eq? t1 t2)
                       (- depth 1)
                       false))))
         (define (lazy-equal? a b depth) (not (eq? false (seq-sexpr-equal? a b depth))))

         (define (lazy-append lazy-lst1 lazy-lst2)
           (if (lazy-null? lazy-lst1)
               lazy-lst2
               (lazy-pair (lazy-lst1 'first) (lazy-append (lazy-lst1 'rest) lazy-lst2))))

         (define (lazy-repeat reps thunk)
           (if (= reps 0)
               lazy-null
               (lazy-pair (thunk) (lazy-repeat (- reps 1) thunk))))

         (define (lazy-map proc lazy-lst)
           (if (lazy-null? lazy-lst)
               lazy-null
               (lazy-pair (proc (lazy-lst 'first)) (lazy-map proc (lazy-lst 'rest)))))

         (define (list->lazy-list lst)
           (if (pair? lst)
               (apply lazy-list (map list->lazy-list lst))
               lst))
                                           
         (define (lazy-list->list a depth)
           (if (= 0 depth)
               (pair 'unf 0)
               (if (lazy-pair? a)
                   (let* ((left (lazy-list->list (a 'first) (- depth 1)))
                          (right (lazy-list->list (a 'rest) (rest left))))
                     (pair (pair (first left) (first right)) (rest right)))
                   (pair a (- depth 1))))))
